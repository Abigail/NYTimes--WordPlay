#!/opt/perl/bin/perl

use 5.010;

use strict;
use warnings;
no  warnings 'syntax';

#
# Take the numeral cards from 0 to 10. Arrange them so that they form
# a stack, then deal the top card up, next card to the bottom of the
# deck, card up, next on the bottom, card up, next on the bottom,
# etc. until all the cards are dealt. If you order them correctly,
# they will come out in the order 0, 1, 2, 3, 4, 5, 6, 7, 8, 9, 10.
# What is the correct order?
#

my $NR_OF_CARDS = shift // 11;

my @copy = my @deck = 0 .. ($NR_OF_CARDS - 1);

my @out;

while (@copy) {
    push @out  => shift @copy;
    push @copy => shift @copy if @copy;
}

my @solution;
   @solution [@out] = @deck;

say "@solution";

__END__
